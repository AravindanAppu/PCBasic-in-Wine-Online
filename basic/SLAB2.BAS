10 COLOR 15,9,5:CLS:KEY OFF:CLEAR:BEEP:BEEP
20 FOR X=1 TO 10
30 PRINT
40 NEXT X
50 PRINT TAB(28)" SLAB DESIGN "
60 FOR X=1 TO 10
70 PRINT
80 NEXT X
90 COLOR 4:PRINT TAB(11)"REFERENCES"
100 COLOR 14:PRINT TAB(10) " 1. I.S 456 - 1978 "
110 PRINT TAB(10) " 2. LIMIT STATE THEORY AND DESIGN OF REINF.CONCRETE "
120 PRINT TAB(15)"     BY S.R.KARVE & VL.SHAH "
130 PRINT
140 COLOR 15:PRINT TAB(65)"  ramji "
150 FOR I = 1 TO 12000
160 NEXT I
170 BEEP:CLS
180 REM SLAB DESIGN
190 PRINT:PRINT:PRINT
210 COLOR 14:INPUT "CLEAR COVER IN mm                             = ",CR
220 INPUT "CHARACTERISTIC STRENGTH OF CONCRETE,STEEL                                                                      (FCK,FY)       = ",FCK,FY
250 EC = 5700*(FCK)^.5
260 FCR = .7 * FCK^.5
270 ES = .2*10^6
280 MO = ES/EC
290 ECS= .0003
300 THETA = 1.6
310 ECE = EC /(1+THETA)
320 MOR = ES/ECE
330 COLOR 2:PRINT TAB(10)"TYPE OF SLABS"
340 COLOR 12:PRINT TAB(10)"SIMPLY SUPPORTED SLAB = 1
350 PRINT TAB(10)"CANTILEVER SLAB       = 2
360 PRINT TAB(10)"CONTINUOUS SLAB       = 3
370 PRINT TAB(10)"TWOWAY SLAB           = 4
380 PRINT
390 PRINT:COLOR 14:INPUT "TYPE OF SLAB ?    .   .   .   .   .   .   .   = ",K
392 INPUT "NAME OF SLAB  .   .   .   .   .   .   .   .   = ",T$
394 INPUT "FLOOR FINISH/WEATHERINGCOURSE +                                                             LIGHT PARTITION (IF ANY)  KN/Sq.M = ",FL
396 INPUT "LIVE LOAD IN KN/Sq.M  (LL)    .   .   .   .   = ",LL
400 IF K=1 THEN 410 ELSE 420
410 GOSUB 500:GOTO 480
420 IF K=2 THEN 430 ELSE 440
430 GOSUB 2110:GOTO 480
440 IF K=3 THEN 450 ELSE 460
450 GOSUB 3640:GOTO 480
460 IF K=4 THEN 470 ELSE 480
470 GOSUB 5730
480 COLOR 12:INPUT "DO YOU WANT TO CONTINUE (Y/N)   .   .   .   . = ", L$
490 IF (L$ ="Y") OR (L$="y") THEN 330 ELSE 9420
500 REM  DESIGN OF SIMPLY SUPPORTED SLAB
510 COLOR 15:PRINT TAB(10)" DESIGN OF SIMPLY SUPPORTED SLAB":COLOR 14
520 INPUT "CLEAR SPAN OF SLAB IN METERS  .   .   .   .   = ",L
530 REM CHECKING THE THICKNESS
540 INPUT "THICKNESS OF SLAB IN mm   .   .   .   .   .   = ",TS
550 TS1=L*1000/20
560 TS2=TS1+5+CR
570 IF TS<TS2 THEN 580 ELSE 620
580 COLOR 15:PRINT "DO CHECK FOR DEFLECTION OR THICKNESS MAY BE INCREASED":COLOR 14
590 INPUT "DO YOU WANT TO INCREASE SLAB THICKNESS ? (Y/N)= ",A$
600 IF(A$="Y") OR(A$= "y" )THEN 610 ELSE 620
610 GOTO 540
620 ED=TS-CR-5
630 SL=(TS)*.025
640 DL=SL+FL
650 TL=DL+LL
660 LX = L + ED/1000
670 INPUT "PARTITION WALL PARALLAL TO SPAN (0/1) .   .   = ",PS
680 IF PS =1 THEN 690 ELSE 780
690 INPUT "HEIGHT OF PARTITION WALL IN METER .   .   .   = ",H
710 DW =.115 +.6*LX
720 PL =.115*H*20/DW
730 M1 = PL *LX^2*.125
740 MU1=1.5*M1
750 MU=MU1:GOSUB 8290
760 ASP=AST
770 GOTO 840
780 INPUT "PARTITION WALL PERPENDICULAR TO SPAN (0/1).   = ",PP
790 IF PP =1 THEN 800 ELSE 840
800 INPUT "HEIGHT OF PARTITION WALL IN METER .   .   .   = ",H
810 INPUT "DIST.OF WALL FROM NEARER SUPPORT IN METER .   = ", A
820 P1 = .115 * H *20
830 M3 =P1 * A * .5
840 M2 = TL * LX^2*.125
850 M4 = M2 + M3
860 MU2 = 1.5 * M4
870 MU=MU2:GOSUB 8290
880 AS=AST
890 REM CHECK FOR DEFLECTION
900 COLOR 2:INPUT "DO YOU WANT CHECK FOR DEFLECTION (Y/N)   .    = ",C$
910 IF (C$="Y" )OR (C$="y" )THEN 920 ELSE 1400
920 REM KL = DL +LL * .25 +PL
925 KL = DL +PL
930 MOP=KL*LX^2 * .125 + M3
940 PT=AS * 10/ED
950 IGR = 1000 * TS^3/12
960 MA = MO: PTA = PT/100:D1= ED:  GOSUB 8990
970  IR = IRA : X = XA: Z = ZA
980 MR = FCR * IGR/(TS/2)
990 MRA = MR: MB = M4:  IRA = IR:D1=ED: GOSUB 9300
1000 IEFT = IEFF
1010 MRA = MR:MB = MOP: IRA = IR:D1 =ED: GOSUB 9300
1020 IEFP = IEFF
1030  REM CALCULATION OF SHORT TERM DEFLECTIONS
1040 R1 = EC * IEFP
1050 R2 = LX^2*10^12
1060 R3 = KL* LX^2/8*R2
1070 R4 = M3 * R2
1080 R5 = EC* IEFT
1090 AIP = 5 *R3 /(48 * R1) +R4/(12 * R1)
1100 R6 = M2 * R2
1110 AIT = 5 * R6 /(48 * R5) + R4/(12 * R5)
1120 AI = AIT - AIP
1130 REM CALCULATION OF SHRINKAGE DEFLECTION
1140 IF PT  < 1  THEN 1150 ELSE 1170
1150 K4 = .72 *(PT)^.5
1160 GOTO 1180
1170 K4 = .65 *(PT)^.5
1180 IF K4 > 1 THEN  K4 = 1
1190 K3 = .125
1200 YCS = K4  * ECS/(TS)
1210 ACS = K3 * YCS * LX^2*10^6
1220 REM DEFLECTION DUE TO CREEP
1230 MA = MOR: PTA = PT/100: D1 = ED :GOSUB 8990
1240 IRC = IRA: XC = XA: ZC = ZA
1250 MRA = MR: MB = MOP: IRA = IRC: Z = ZC: X = XC:GOSUB 9300
1260 IEFC = IEFF
1270 R7 = ECE * IEFC
1280 AICP = 5 * R3/(48 * R7) + R4/(12 * R7)
1290 ACP = AICP - AIP
1300 REM TOTAL DEFLECTION
1310 AT = AIT + ACP +ACS
1320 ADFL = LX * 1000 /250
1330 IF AT <ADFL GOTO 1340 ELSE 1360
1340 CLS:COLOR 2:PRINT  " DEPTH PROVIDED IS OK FOR DEFLECTION "
1350 GOTO  1400
1360 COLOR 4:PRINT  "CHANGE THE DEPTH FOR DEFLECTION "
1370 COLOR 15:PRINT "ALLOWABLE DEFLECTION      .   .   .   .   .   = ";ADFL
1380 PRINT "ANTICIPATED DEFLECTION    .   .   .   .   .   = ";AT
1390 GOTO 2100
1400 COLOR 14:PRINT TAB(12) "DESIGN OF SIMPLY SUPPORTED SLAB (FLOOR/ROOF) = ";T$
1410 PRINT
1420 PRINT  TAB(40)"CLEAR SPAN     = ";:PRINT  USING  "##.##";L;:PRINT  " M"
1430 PRINT  TAB(40)"SLAB THICKNESS = "; TS;:PRINT  "MM"
1440 PRINT  TAB(40)"CLEAR COVER    = "; CR;:PRINT  "MM"
1450 PRINT  TAB(2)"LOADINGS "
1460 PRINT  TAB(7)"SELF WEIGHT .   .   .   .   .   .  =";:PRINT  USING "##.##";SL;:PRINT "  Kn/Sq.M"
1470 PRINT  TAB(7)"FLOOR FINISH/WEATHERING COURSE "
1480 PRINT  TAB(12)"+ LIGHT PARTITION (IF ANY)  . =";:PRINT  USING "##.##";FL;:PRINT "  Kn/Sq.M"
1490 PRINT  TAB(7)"LIVE LOAD   .   .   .   .    .     =";:PRINT  USING "##.##";LL;:PRINT "  Kn/Sq.M"
1500 PRINT  TAB(12)"TOTAL LOAD   .   .   .   .    =";:PRINT  USING "##.##";TL;:PRINT "  Kn/Sq.M"
1510 PRINT TAB(2) "CHARACTERISTIC STRENGTH OF "
1520 PRINT TAB(7) "CONCRETE , STEEL .   .   .   .   . =";FCK;:PRINT "  ; ";:PRINT ;FY
1530 PRINT TAB(2)"DESIGN"
1540 PRINT TAB(2)"BENDING MOMENT ";:PRINT TAB(25);:PRINT USING "##.##";MU2
1550 PRINT TAB(9)  "( MU ) "
1560 PRINT TAB(2)"AREA OF STEEL ";:PRINT TAB(25);:PRINT USING "##.##";AS
1570 PRINT TAB(9) " ( Sq.CM )
1580 PRINT
1590 IF PS=1 THEN 1600 ELSE 1620
1600 PRINT TAB(2)"DISPERSION WIDTH ";:PRINT TAB(25);:PRINT USING "##.##";DW
1610 PRINT TAB(2)"AREA OF STEEL ";:PRINT TAB(25);:PRINT USING "##.##";ASP
1620 IF (C$="Y" )OR (C$="y" )THEN 1630 ELSE 1640
1630 PRINT "ALLOWABLE DEFLECTION  ";:PRINT TAB(27);:PRINT USING "##.##";ADFL;:PRINT "  MM"
1640 PRINT "ANTICIPATED DEFLECTION  ";:PRINT TAB(27);:PRINT USING "##.##";AT;:PRINT "  MM"
1650 COLOR 15:INPUT "DO YOU WANT PRINT OUT (Y\N)     = ",D$
1660 IF (D$="Y" )OR (D$="y" )THEN 1670 ELSE 2100
1670 LPRINT  CHR$(27)"!"CHR$(0);:LPRINT CHR$(27)"~80";:LPRINT  CHR$(27)"I"CHR$(5);:LPRINT  CHR$(27)"Q"CHR$(80)
1680 LPRINT  TAB(12) "DESIGN OF SIMPLY SUPPORTED SLAB (FLOOR/ROOF) = ";T$
1690 LPRINT
1700 LPRINT  TAB(40)"CLEAR SPAN     = ";:LPRINT  USING  "##.##";L;:LPRINT  " M"
1710 LPRINT  TAB(40)"SLAB THICKNESS = "; TS;:LPRINT  "MM"
1720 LPRINT  TAB(40)"CLEAR COVER    = "; CR;:LPRINT  "MM"
1730 LPRINT  TAB(2)"LOADINGS "
1740 LPRINT
1750 LPRINT  TAB(7)"SELF WEIGHT .   .   .   .   .   .  =";:LPRINT  USING "##.##";SL;:LPRINT "  Kn/Sq.M"
1760 LPRINT  TAB(7)"FLOOR FINISH/WEATHERING COURSE "
1770 LPRINT  TAB(12)"+ LIGHT PARTITION (IF ANY)  . =";:LPRINT  USING "##.##";FL;:LPRINT "  Kn/Sq.M"
1780 LPRINT  TAB(7)"LIVE LOAD   .   .   .   .    .     =";:LPRINT  USING "##.##";LL;:LPRINT "  Kn/Sq.M"
1790 LPRINT
1800 LPRINT  TAB(12)"TOTAL LOAD   .   .   .   .    =";:LPRINT  USING "##.##";TL;:LPRINT "  Kn/Sq.M"
1810 LPRINT
1820 LPRINT
1830 LPRINT TAB(2) "CHARACTERISTIC STRENGTH OF "
1840 LPRINT TAB(7) "CONCRETE , STEEL .   .   .   .   . =";FCK;:LPRINT "  ; ";:LPRINT ;FY
1842 IF PP = 1 THEN 1845 ELSE 1850
1845 LPRINT TAB(2) "POINT LOAD              = ";:LPRINT USING "###.##"; P1;:LPRINT " Kn\M"
1847 LPRINT TAB(2) "DIST.FROM NEAR SUPPORT  = ";:LPRINT USING "###.##";A;:LPRINT " M"
1850 LPRINT
1860 LPRINT
1870 LPRINT TAB(2)"DESIGN"
1880 LPRINT
1890 LPRINT TAB(2)"BENDING MOMENT ";:LPRINT TAB(25);:LPRINT USING "##.##";MU2;:LPRINT " Kn.M "
1900 LPRINT TAB(9)  "( MU ) "
1910 LPRINT TAB(2)"AREA OF STEEL ";:LPRINT TAB(25);:LPRINT USING "##.##";AS;:LPRINT " Sq.CM "
1920 REM LPRINT TAB(9) " ( Sq.CM )
1922 IF PS=1 THEN 1924 ELSE 1930
1924 LPRINT TAB(2)"DISPERSION WIDTH ";:LPRINT TAB(25);:LPRINT USING "##.##";DW;:LPRINT " M"
1928 LPRINT TAB(2)"AREA OF STEEL ";:LPRINT TAB(25);:LPRINT USING "##.##";ASP;:LPRINT " Sq.CM"
1930 LPRINT
1940 LPRINT
1950 LPRINT TAB(2) "REINFORCEMENT "
1960 LPRINT TAB(2) "DETAILS "
1970 LPRINT TAB(2) "(Main bars) "
2020 LPRINT
2030 LPRINT
2040 LPRINT TAB(2) "DISTRIBUTION STEEL  "
2050 LPRINT
2060 LPRINT
2070 IF (C$="Y" )OR (C$="y" )THEN 2080 ELSE 2100
2080 LPRINT "ALLOWABLE DEFLECTION  ";:LPRINT TAB(27);:LPRINT USING "##.##";ADFL;:LPRINT "  MM"
2090 LPRINT "ANTICIPATED DEFLECTION  ";:LPRINT TAB(27);:LPRINT USING "##.##";AT;:LPRINT "  MM"
2100 RETURN
2110 REM  DESIGN OF CANTILEVER SLAB
2120 COLOR 15:PRINT TAB(10) "DESIGN OF CANTILEVER SLAB"
2130 PRINT
2140 COLOR 14:INPUT "SPAN OF SLAB IN METERS .    .   .   .   .    = ",L
2150 REM CHECKING THE THICKNESS
2160 INPUT "THICKNESS OF SLAB AT SUPPORT,END IN mm    .  = ",TS,TSE
2170 TS1=L*1000/9
2180 TS2=TS1+5+CR
2190 IF TS<TS2 THEN 2200 ELSE 2240
2200 COLOR 15:PRINT TAB(10)"DO CHECK FOR DEFLECTION OR THICKNESS MAY BE INCREASED"
2210 COLOR 14:INPUT "DO YOU WANT TO INCREASE SLAB THICKNESS (Y/N)=",A$
2220 IF(A$="Y") OR(A$= "y" )THEN 2230 ELSE 2240
2230 GOTO 2160
2240 ED1=TS-CR-5
2250 SL=(TS+TSE)*.0125
2260 DL=SL+FL
2270 TL=DL+LL
2280 INPUT "POINT LOADS (MAX.2 Nos) AND DISTANCES IN METERS(P1,X1,P2,X2) =", P1,X1,P2,X2
2290 MOS= TL*L^2/2+P1*X1+P2*X2
2300 MUS=1.5*MOS
2310 ED=ED1:MU=MUS:GOSUB 8290
2320 ASS=AST
2330 TSM =TSE+(TS-TSE)/2
2340 ED2=TSM-CR-5
2350 X3=X1-(L/2)
2360 IF X3<0 THEN X3=0
2370 X4=X2-(L/2)
2380 IF X4<0 THEN X4=0
2390 MOM=TL*L^2/8+P1*X3+P2*X4
2400 MUM=1.5*MOM
2410 ED=ED2:MU=MUM:GOSUB 8290
2420 ASM=AST
2430 REM CHECK FOR DEFLECTION
2440 COLOR 2:INPUT "DO YOU WANT CHECK FOR DEFLECTION (Y/N)   .   = ",C$
2450 IF (C$="Y" )OR (C$="y" )THEN 2460 ELSE 2950
2460 KL = DL +LL * .25
2470 MOP=KL*L^2/2+P1*X1+P2*X2
2480 PT=ASS * 10/ED1
2490 IGR = 1000*(TS)^3/12
2500 MR = FCR * IGR/(TS/2)
2510 MA = MO: PTA = PT/100:D1= ED1:  GOSUB 8990
2520  IR = IRA : X = XA: Z = ZA
2530 MRA = MR: MB = MOS:  IRA = IR:D1=ED1: GOSUB 9300
2540 IEFT = IEFF
2550 MRA = MR:MB = MOP: IRA = IR:D1 =ED1: GOSUB 9300
2560 IEFP = IEFF
2570  REM CALCULATION OF SHORT TERM DEFLECTIONS
2580 R1 = EC * IEFP
2590 R2 = KL * L^4*10^12
2600 R3 = P1 * X1^3*10^12
2610 R4 = P2 * X2^3 * 10^12
2620 R5 = 3 * R1
2630 AIP = R2/ (8 * R1) +R3/R5 + R4 /R5
2640 R6 = EC * IEFT
2650 R7 = 3 * R6
2660 AIT = TL *L^4*10^12/(8*R6) + R3/R7 + R4/R7
2670 AI = AIT - AIP
2680 REM CALCULATION OF SHRINKAGE DEFLECTION
2690 IF PT  < 1  THEN 2700 ELSE 2720
2700 K4 = .72 *(PT)^.5
2710 GOTO 2730
2720 K4 = .65 *(PT)^.5
2730 IF K4 > 1 THEN  K4 = 1
2740 K3 = .5
2750 YCS = K4  * ECS/(TS)
2760 ACS = K3 * YCS * L^2*10^6
2770 REM DEFLECTION DUE TO CREEP
2780 MA = MOR: PTA = PT/100: GOSUB 8990
2790 IRC = IRA: XC = XA: ZC = ZA
2800 MRA = MR: MB = MOP: IRA = IRC: Z = ZC: X = XC:GOSUB 9300
2810 IEFC = IEFF
2820 R6 = ECE * IEFC
2830 AICP = R2/(8*R6)+R3/(3*R6)+R4/(3*R6)
2840 ACP = AICP - AIP
2850 REM TOTAL DEFLECTION
2860 AT = AIT + ACP +ACS
2870 ADFL = L * 1000 /250
2880 IF AT <ADFL GOTO 2890 ELSE 2910
2890 CLS:COLOR 2:PRINT TAB(10) " DEPTH PROVIDED IS OK FOR DEFLECTION "
2900 GOTO  2950
2910 COLOR 4:PRINT TAB(10) "CHANGE THE DEPTH FOR DEFLECTION "
2920 COLOR 15:PRINT  "ALLOWABLE DEFLECTION   .   .   .   .   .   .= ", ADFL
2930 PRINT  "ANTICIPATED DEFLECTION .   .   .   .   .   .= ", AT
2940 GOTO 3630
2950 COLOR 14:PRINT  TAB(12) "DESIGN OF CANTILEVER SLAB (FLOOR/ROOF) = ";T$
2970 PRINT
2980 PRINT  TAB(37)"CLEAR SPAN       = ";:PRINT  USING  "##.##";L;:PRINT  " M"
2990 PRINT  TAB(37)"SLAB THICKNESS  "
3000 PRINT  TAB(41)"AT SUPPORT   = "; TS;:PRINT  "MM"
3010 PRINT  TAB(37)"SLAB THICKNESS  "
3020 PRINT  TAB(41)"AT END       = "; TSE;:PRINT  "MM"
3030 PRINT  TAB(37)"CLEAR COVER      = "; CR;:PRINT  "MM"
3040 PRINT  TAB(2)"LOADINGS "
3050 PRINT  TAB(7)"SELF WEIGHT .   .   .   .   .   .  =";:PRINT  USING "##.##";SL;:PRINT "  Kn/Sq.M"
3060 PRINT  TAB(7)"FLOOR FINISH/WEATHERING COURSE "
3070 PRINT  TAB(12)"+ LIGHT PARTITION (IF ANY)  . =";:PRINT  USING "##.##";FL;:PRINT "  Kn/Sq.M"
3080 PRINT  TAB(7)"LIVE LOAD   .   .   .   .    .     =";:PRINT  USING "##.##";LL;:PRINT "  Kn/Sq.M"
3090 PRINT  TAB(12)"TOTAL LOAD   .   .   .   .    =";:PRINT  USING "##.##";TL;:PRINT "  Kn/Sq.M"
3100 PRINT
3110 PRINT TAB(2) "CHARACTERISTIC STRENGTH OF "
3120 PRINT TAB(7) "CONCRETE , STEEL .   .   .   .   . =";FCK;:PRINT "  ; ";:PRINT ;FY
3130 PRINT TAB(2)"DESIGN"
3140 PRINT TAB(2)"BENDING MOMENT ";:PRINT TAB(25);:PRINT USING "##.##";MUS;:PRINT TAB(50);:PRINT USING "##.##";MUM
3150 PRINT TAB(9)  "( MU ) "
3160 PRINT TAB(2)"AREA OF STEEL ";:PRINT TAB(25);:PRINT USING "##.##";ASS;:PRINT TAB(50);:PRINT USING "##.##";ASM
3170 PRINT
3180 PRINT TAB(2) "REINFORCEMENT "
3190 IF (C$="Y" )OR (C$="y" )THEN 3200 ELSE 3220
3200 PRINT "ALLOWABLE DEFLECTION  ";:PRINT TAB(27);:PRINT USING "##.##";ADFL;:PRINT "  MM"
3210 PRINT "ANTICIPATED DEFLECTION  ";:PRINT TAB(27);:PRINT USING "##.##";AT;:PRINT "  MM"
3220 COLOR 15:INPUT "DO YOU WANT PRINT OUT (Y\N)    =",D$
3230 IF (D$="Y" )OR (D$="y" )THEN 3240 ELSE 3630
3240 LPRINT  CHR$(27)"!"CHR$(0);:LPRINT CHR$(27)"~80";:LPRINT  CHR$(27)"I"CHR$(5);:LPRINT  CHR$(27)"Q"CHR$(80)
3250 LPRINT  TAB(12) "DESIGN OF CANTILEVER SLAB (FLOOR/ROOF) = ";T$
3260 LPRINT
3270 LPRINT  TAB(37)"CLEAR SPAN       = ";:LPRINT  USING  "##.##";L;:LPRINT  " M"
3280 LPRINT  TAB(37)"SLAB THICKNESS  "
3290 LPRINT  TAB(41)"AT SUPPORT   = "; TS;:LPRINT  "MM"
3300 LPRINT  TAB(37)"SLAB THICKNESS  "
3310 LPRINT  TAB(45)"AT END   = "; TSE;:LPRINT  "MM"
3320 LPRINT  TAB(37)"CLEAR COVER      = "; CR;:LPRINT  "MM"
3330 LPRINT  TAB(2)"LOADINGS "
3340 LPRINT
3350 LPRINT  TAB(7)"SELF WEIGHT .   .   .   .   .   .  =";:LPRINT  USING "##.##";SL;:LPRINT "  Kn/Sq.M"
3360 LPRINT  TAB(7)"FLOOR FINISH/WEATHERING COURSE "
3370 LPRINT  TAB(12)"+ LIGHT PARTITION (IF ANY)  . =";:LPRINT  USING "##.##";FL;:LPRINT "  Kn/Sq.M"
3380 LPRINT  TAB(7)"LIVE LOAD   .   .   .   .    .     =";:LPRINT  USING "##.##";LL;:LPRINT "  Kn/Sq.M"
3390 LPRINT
3400 LPRINT  TAB(12)"TOTAL LOAD   .   .   .   .    =";:LPRINT  USING "##.##";TL;:LPRINT "  Kn/Sq.M"
3410 LPRINT
3420 LPRINT
3430 LPRINT TAB(2) "CHARACTERISTIC STRENGTH OF "
3440 LPRINT TAB(7) "CONCRETE , STEEL .   .   .   .   . =";FCK;:LPRINT "  ; ";:LPRINT ;FY
3450 LPRINT
3460 LPRINT
3470 LPRINT TAB(2)"DESIGN"
3480 LPRINT
3485 LPRINT TAB(25)"SUPPORT";:LPRINT TAB(50)"MID SPAN"
3486 LPRINT
3490 LPRINT TAB(2)"BENDING MOMENT ";:LPRINT TAB(25);:LPRINT USING "##.##";MUS;:LPRINT TAB(50);:LPRINT USING "##.##";MUM
3500 LPRINT TAB(9)  "( MU ) "
3510 LPRINT TAB(2)"AREA OF STEEL ";:LPRINT TAB(25);:LPRINT USING "##.##";ASS;:LPRINT TAB(50);:LPRINT USING "##.##";ASM
3515 LPRINT TAB(9)"  ( Sq.CM )
3520 LPRINT
3530 LPRINT TAB(2) "REINFORCEMENT "
3540 LPRINT TAB(2) "DETAILS "
3550 LPRINT TAB(2) "(Main bars) "
3560 LPRINT
3570 LPRINT TAB(2) "DISTRIBUTION STEEL  "
3580 LPRINT
3590 LPRINT
3600 IF (C$="Y" )OR (C$="y" )THEN 3610 ELSE 3630
3610 LPRINT "ALLOWABLE DEFLECTION  ";:LPRINT TAB(27);:LPRINT USING "##.##";ADFL;:LPRINT "  MM"
3620 LPRINT "ANTICIPATED DEFLECTION  ";:LPRINT TAB(27);:LPRINT USING "##.##";AT;:LPRINT "  MM"
3630 RETURN
3640 REM DESIGN OF ONE WAY CONTINOUS SLAB
3650 COLOR 15:PRINT TAB(10) "DESIGN OF ONE WAY CONTIONOUS SLAB"
3660 COLOR 14:INPUT "NO OF SPANS   .   .   .   .   .   .   .   .   = ",NOS
3670 IF NOS <= 2 THEN 3940 ELSE  3680
3680 INPUT "CLEAR SPAN IN METERS L1,L2,L3   .   .    .    = ",LX1,LX2,LX3
3690 LX= (LX1+LX2+LX3)/3
3700 INPUT "CLEAR SPAN LY IN (METER)   .   .   .   .   .  = ",LY
3710 IF (LY/LX) < 2 THEN 3720 ELSE 3740
3720 COLOR 12:PRINT TAB(10) "DESIGN AS TWO WAY SLAB"
3730 GOTO 5720
3740 REM CHECKING THE THICKNESS OF SLAB
3750 INPUT "WIDTH OF SUPPORT IN mm   .   .   .   .   .    = ",B
3760 INPUT "THICKNESS OF SLAB IN mm   .   .   .   .   .   = ",TS
3770 TS1=(LX*1000/33)
3780 TS2=TS1+5+CR
3790 IF TS < TS2 THEN  3800 ELSE 3840
3800 COLOR 15:PRINT  "DO CHECK FOR DEFLECTION OR THICKNESS OF SLAB MAY BE INCREASED"
3810 COLOR 14:INPUT "DO YOU WANT TO INCREASE SLAB THICKNESS (Y\N)  = ",A$
3820 IF (A$="Y") OR (A$="y") THEN 3830 ELSE 3840
3830 GOTO 3760
3840 ED=TS-CR-5
3850 IF B> ((LX/12)*1000) THEN 3860 ELSE 3880
3860 ES1=LX1: ES2=LX2: ES3=LX3
3870 GOTO 3890
3880 ES1=LX1+ED/1000:ES2=LX2+ED/1000:ES3=LX3+ED/1000
3890 SL=TS*.025
3900 DL=SL+FL
3910 TL = DL + LL
3920 IF (ES1 > (1.15*ES2)) OR (ES1 < (.85*ES2)) THEN 3940 ELSE 3960
3930 IF (ES2 > (1.15*ES3)) OR (ES2 < (.85*ES3)) THEN 3940  ELSE 3960
3940 COLOR 4:PRINT TAB(10) "CHOOSE MOMENT DISTRIBUTION METHOD"
3950 GOTO  5720
3960 DLM1=DL*(ES1^2)/12
3970 DLM2=DL*ES2^2/24
3980 DLM3=DL*((ES1+ES2)/2)^2/10
3990 DLM4=DL*((ES2+ES3)/2)^2/12
4000 LLM1=LL*ES1^2/10
4010 LLM2=LL*ES2^2/12
4020 LLM3=LL*((ES1+ES2)/2)^2/9
4030 LLM4=LL*((ES2+ES3)/2)^2/9
4040 MU1=1.5*(DLM1+LLM1)
4050 MU2=1.5*(DLM2+LLM2)
4060 MU3=1.5*(DLM3+LLM3)
4070 MU4=1.5*(DLM4+LLM4)
4080 INPUT "IS PARTITION WALL PARALLAL TO SPAN (1 OR 0)   = ",P
4090 IF P = 1 THEN 4100 ELSE 4170
4100 DW = .115 + .6*LX
4110 INPUT "HEIGHT OF PARTITION WALL IN METER .   .   .   = ",H
4120 PL = .115*H*20/DW
4130 AM1 = 1.5*PL*(ES1^2)/12
4140 AM2 = 1.5*PL*(ES2^2)/24
4150 AM3 = 1.5*PL*((ES1+ES2)/2)^2/10
4160 AM4 = 1.5*PL*((ES2+ES3)/2)^2/12
4170 MU5=MU1 + AM1
4180 MU6=MU2 + AM2
4190 MU7=MU3 + AM3
4200 MU8=MU4 + AM4
4210 MU = MU5:GOSUB 8290
4220 ASEM = AST
4230 MU = MU6:GOSUB 8290
4240 ASMM = AST
4250 MU = MU7:GOSUB 8290
4260 ASES = AST
4270 MU = MU8:GOSUB 8290
4280 ASMS = AST
4290 RAT =DL/TL
4300 REM CHECK FOR DEFLECTION
4310 COLOR 14:INPUT  "DO YOU WANT TO CHECK FOR DEFLECTION (Y/N)    = ",C$
4320 IF (C$ ="Y") OR (C$ ="y") THEN  4330 ELSE 5040
4330 REM DEFLECTION FOR END SPAN
4340 MF1=(TL + PL) * ES1^2/12: MF2 = MF1
4350 L=ES1:M1=0:M2=MU5/1.5:M3=MU7/1.5:M4=0:M5=DLM1 + (AM1/1.5):M6=DLM3 + (AM3/1.5):MF3=((RAT * TL) +PL) *ES1^2/12:MF4=MF3
4360 PT1=0:PT2=ASEM/(10 * ED):PT3=ASES/(10 * ED):D1=ED:K3=.086:GOSUB 4510
4370 ATE = AT
4380 REM DEFLECTION FOR MID SPAN
4390 MF1=(TL+PL)*ES2^2/12:MF2=MF1
4400 L=ES2:M1=MU7/1.5:M2=MU6/1.5:M3=MU8/1.5:M4=DLM3 + (AM3/1.5):M5=DLM2 + (AM2/1.5):M6=DLM4 + (AM4/1.5):MF3=((RAT * TL) +PL) *ES2^2/12:MF4=MF3
4410 PT1=ASES/(10* ED):PT2=ASMM/(10 * ED):PT3=ASMS/(10 * ED):D1=ED:K3=.0625: GOSUB 4510
4415 ATM = AT
4430 ADFL = ES1 * 1000/250
4440 IF ATE >ADFL  THEN 4450 ELSE 4490
4450 COLOR 2:PRINT TAB(10) "CHANGE THE DEPTH OF SLAB FOR DEFLECTION "
4460 COLOR 15:PRINT TAB(10) "ALLOWABLE DFFLECTION      = ", ADFL
4470 PRINT TAB(10) "ANTICIPATED DFFLECTION      = ", ATE
4480 GOTO 5720
4490 CLS:COLOR 2:PRINT TAB(10) "DEPTH OF SLAB IS OK FOR DEFLECTION "
4500 GOTO 5040
4510 REM DEFLECTION CALCULATION
4520 IGR = 1000*(TS)^3/12
4530 MR = FCR * IGR/(TS/2)
4540 MA = MO: PTA = PT1:  GOSUB 8990
4550 IRL = IRA : XL = XA: ZL = ZA
4560 MA = MO : PTA = PT2 :  GOSUB 8990
4570 IRM = IRA :XM = XA: ZM = ZA
4580 MA = MO : PTA = PT3 :  GOSUB 8990
4590 IRR = IRA :XR = XA: ZR = ZA
4600 AK2 = (M1+M3)*(1/(MF1+MF2))
4610 IF AK2 <= .5 THEN  4620 ELSE 4630
4620 AK1 = 0: GOTO 4650
4630 K2 = AK2: GOSUB 9070
4640 AK1 = K1
4650 IRE = AK1 * ((IRL + IRR)/2) + (1-AK1)*IRM
4660 MRA = MR: MB = M2: Z = ZM: X = XM: IRA = IRE: GOSUB 9300
4670 IEFT = IEFF
4680 BK2 = (M4 + M6)*(1/(MF3 +MF4))
4690 IF BK2 <= .5 THEN 4700 ELSE  4710
4700 BK1 = 0 : GOTO  4730
4710 K2  = BK2 :GOSUB 9070
4720 BK1 = K1
4730 IRP = BK1 * (IRL + IRR)/2 + (1-BK1) * IRM
4740 MRA = MR:MB = M5: Z = ZM: X= XM: IRA = IRP:D1 = D1: GOSUB 9300
4750 IEFP = IEFF
4760  REM CALCULATION OF SHORT TERM DEFLECTIONS
4770  AIT = (L^2 * 10^12 /(EC *IEFT)) * ((5/48) * M2 -(M1/16)-(M3/16))
4780  AIP = (L^2 * 10^12 /(EC *IEFP)) * ((5/48) * M5 -(M4/16)-(M6/16))
4790 AI = AIT - AIP
4800 REM CALCULATION OF SHRINKAGE DEFLECTION
4810 PTM = PT2 * 100
4820 IF PTM  < 1  THEN 4830 ELSE 4850
4830 K4 = .72 *(PTM^.5)
4840 GOTO 4860
4850 K4 = .65 *(PTM^.5)
4860 IF K4 > 1 THEN  K4 = 1
4870 YCS = K4  * ECS/TS
4880 ACS = K3 * YCS * L^2*10^6
4890 REM DEFLECTION DUE TO CREEP
4900 MA = MOR: PTA = PT1:GOSUB 8990
4910 IRCL = IRA: XCL = XA: ZCL = ZA
4920 MA = MOR: PTA = PT2: GOSUB 8990
4930 IRCM = IRA: XCM = XA :ZCM = ZA
4940 MA = MOR: PTA = PT3:GOSUB 8990
4950 IRCR = IRA: XCR = XA:ZCR = ZA
4960 IRCE = BK1 * (IRCL + IRCR)/2 + (1-BK1) * IRCM
4970 MRA = MR: MB = M5 : IRA = IRCE: Z = ZCM: X = XCM:GOSUB 9300
4980 IEFC = IEFF
4990  AICP =(L^2*10^12/(ECE*IEFC))* ((5/48)*M5 - (M4/16) -(M6/16))
5000 ACP = AICP - AIP
5010 REM TOTAL DEFLECTION
5020 AT = AIT + ACP +ACS
5030 RETURN
5040 COLOR 14:PRINT  TAB(12) "DESIGN OF CONTINOUS SLAB (FLOOR/ROOF) = ";T$
5050 PRINT  TAB(35)"CLEAR SPAN     = ";:PRINT USING  "##.##";LX1;:PRINT;" X";:PRINT USING "##.##";LX2;:PRINT;" X";:PRINT USING "##.##";LX3;:PRINT ;" M "
5060 PRINT  TAB(35)"SLAB THICKNESS = ";TS;:PRINT "MM"
5070 PRINT  TAB(35)"CLEAR COVER    = "; CR;:PRINT  " MM"
5080 PRINT  TAB(2)"LOADINGS "
5090 PRINT  TAB(7)"SELF WEIGHT .   .   .   .   .   .  =";:PRINT  USING "##.##";SL;:PRINT "  Kn/Sq.M"
5100 PRINT  TAB(7)"FLOOR FINISH/WEATHERING COURSE "
5110 PRINT  TAB(12)"+ LIGHT PARTITION (IF ANY)  . =";:PRINT  USING "##.##";FL;:PRINT "  Kn/Sq.M"
5120 PRINT  TAB(7)"LIVE LOAD   .   .   .   .    .     =";:PRINT  USING "##.##";LL;:PRINT "  Kn/Sq.M"
5130 PRINT  TAB(12)"TOTAL LOAD   .   .   .   .    =";:PRINT  USING "##.##";TL;:PRINT "  Kn/Sq.M"
5140 PRINT TAB(2) "CHARACTERISTIC STRENGTH OF "
5150 PRINT TAB(7) "CONCRETE , STEEL .   .   .   .   . =";FCK;:PRINT "  ; ";:PRINT ;FY
5160 PRINT
5170 PRINT TAB(2)"DESIGN"
5180 PRINT TAB(27)"END SPAN ";:PRINT TAB(52)"INTER.SPAN"
5190 PRINT TAB(22)"MIDSPAN ";:PRINT TAB(32)"SUPPORT";:PRINT TAB(47)"MID SPAN";:PRINT TAB(57)"SUPPORT"
5200 PRINT TAB(2)"BENDING MOMENT ";:PRINT TAB(22);:PRINT USING "##.##";MU5;:PRINT TAB(34);:PRINT USING "##.##";MU7;:PRINT TAB(49);:PRINT USING "##.##";MU6;:PRINT TAB(57);:PRINT USING "##.##";MU8
5210 PRINT TAB(9)  "( MU ) "
5220 PRINT TAB(2)"AREA OF STEEL  ";:PRINT TAB(22);:PRINT USING "##.##";ASEM;:PRINT TAB(34);:PRINT USING "##.##";ASES;:PRINT TAB(49);:PRINT USING "##.##";ASMM;:PRINT TAB(57);:PRINT USING "##.##";ASMS
5230 PRINT TAB(7)"( Ast ) Sq.CM"
5240 IF (C$="Y" )OR (C$="y" )THEN 5250 ELSE 5290
5250 PRINT "ALLOWABLE DEFLECTION  ";:PRINT TAB(27);:PRINT USING "##.##";ADFL;:PRINT "  MM"
5260 PRINT "ANTICIPATED DEFLECTION  "
5270 PRINT TAB(7)"END SPAN";:PRINT TAB(27);:PRINT USING "##.##";ATE;:PRINT "  MM"
5280 PRINT TAB(7)"INTER. SPAN";:PRINT TAB(27);:PRINT USING "##.##";ATM;:PRINT "  MM"
5290 COLOR 15:INPUT "DO YOU WANT PRINT OUT (Y/N)        =",D$
5300 IF (D$="Y" )OR (D$="y" )THEN 5310 ELSE 5720
5310 LPRINT  CHR$(27)"!"CHR$(0);:LPRINT CHR$(27)"~80";:LPRINT  CHR$(27)"I"CHR$(5);:LPRINT  CHR$(27)"Q"CHR$(80)
5320 LPRINT  TAB(12) "DESIGN OF CONTINOUS SLAB (FLOOR/ROOF) = ";T$
5330 LPRINT
5340 LPRINT  TAB(35)"CLEAR SPAN     = ";:LPRINT  USING  "##.##";LX1;:LPRINT;" X";:LPRINT USING "##.##";LX2;:LPRINT;" X";:LPRINT USING "##.##";LX3;:LPRINT ;" M "
5350 LPRINT  TAB(35)"SLAB THICKNESS = ";TS;:LPRINT "MM"
5360 LPRINT  TAB(35)"CLEAR COVER    = "; CR;:LPRINT  " MM"
5370 LPRINT  TAB(2)"LOADINGS "
5380 LPRINT
5390 LPRINT  TAB(7)"SELF WEIGHT .   .   .   .   .   .  =";:LPRINT  USING "##.##";SL;:LPRINT "  Kn/Sq.M"
5400 LPRINT  TAB(7)"FLOOR FINISH/WEATHERING COURSE "
5410 LPRINT  TAB(12)"+ LIGHT PARTITION (IF ANY)  . =";:LPRINT  USING "##.##";FL;:LPRINT "  Kn/Sq.M"
5420 LPRINT  TAB(7)"LIVE LOAD   .   .   .   .    .     =";:LPRINT  USING "##.##";LL;:LPRINT "  Kn/Sq.M"
5430 LPRINT
5440 LPRINT  TAB(12)"TOTAL LOAD   .   .   .   .    =";:LPRINT  USING "##.##";TL;:LPRINT "  Kn/Sq.M"
5450 LPRINT
5460 LPRINT
5470 LPRINT TAB(2) "CHARACTERISTIC STRENGTH OF "
5480 LPRINT TAB(7) "CONCRETE , STEEL .   .   .   .   . =";FCK;:LPRINT "  ; ";:LPRINT ;FY
5490 LPRINT
5500 LPRINT
5510 LPRINT TAB(2)"DESIGN"
5520 LPRINT
5530 LPRINT TAB(27)"END SPAN ";:LPRINT TAB(52)"INTER.SPAN"
5540 LPRINT TAB(22)"MIDSPAN ";:LPRINT TAB(32)"SUPPORT";:LPRINT TAB(47)"MID SPAN";:LPRINT TAB(57)"SUPPORT"
5550 LPRINT TAB(2)"BENDING MOMENT ";:LPRINT TAB(22);:LPRINT USING "##.##";MU5;:LPRINT TAB(34);:LPRINT USING "##.##";MU7;:LPRINT TAB(49);:LPRINT USING "##.##";MU6;:LPRINT TAB(57);:LPRINT USING "##.##";MU8
5560 LPRINT TAB(9)  "( MU ) "
5570 LPRINT TAB(2)"AREA OF STEEL  ";:LPRINT TAB(22);:LPRINT USING "##.##";ASEM;:LPRINT TAB(34);:LPRINT USING "##.##";ASES;:LPRINT TAB(49);:LPRINT USING "##.##";ASMM;:LPRINT TAB(57);:LPRINT USING "##.##";ASMS
5580 LPRINT TAB(7)"( Ast ) Sq.CM"
5590 LPRINT
5600 LPRINT TAB(2) "REINFORCEMENT "
5610 LPRINT TAB(2) "DETAILS "
5620 LPRINT TAB(2) "(Main bars) "
5630 LPRINT
5640 LPRINT TAB(2) "DISTRIBUTION STEEL  "
5650 LPRINT
5660 LPRINT
5670 IF (C$="Y" )OR (C$="y" )THEN 5680 ELSE 5720
5680 LPRINT "ALLOWABLE DEFLECTION  ";:LPRINT TAB(27);:LPRINT USING "##.##";ADFL;:LPRINT "  MM"
5690 LPRINT "ANTICIPATED DEFLECTION  "
5700 LPRINT TAB(7)"END SPAN";:LPRINT TAB(27);:LPRINT USING "##.##";ATE;:LPRINT "  MM"
5710 LPRINT TAB(7)"INTER. SPAN";:LPRINT TAB(27);:LPRINT USING "##.##";ATM;:LPRINT "  MM"
5720 RETURN
5730  REM  TWO WAY RESTRAINED SLAB DESIGN  AS PER I.S:456-1978
5740  COLOR 15:PRINT TAB(10) "DESIGN OF TWO WAY SLAB FLOOR/ROOF "
5750  COLOR 14:INPUT " CLEAR SPAN  IN X DIRECTION IN METERS         = ", LX1
5760  INPUT " CLEAR SPAN  IN Y DIRECTION IN METERS         = ", LY1
5770  INPUT " WIDTH OF SUPPORT IN mm ( X-DIRECTION)  WS1   = ", WS1
5780 INPUT "THICKNESS OF SLAB IN mm                       = ", TS
5790  REM CHECKING THE THICKNESS OF SLAB
5800 TS1 = LX1* 1000/32
5810 IF  TS  < TS1 THEN  5820 ELSE  5870
5820 COLOR 15:PRINT  " DO CHECK FOR DEFLECTION OR THICKNESS OF SLAB MAY BE INCREASED"
5830 COLOR 14:PRINT  "DO YOU WANT TO INCREASE THE SLAB THICKNESS (Y\N) = ",A$
5840 IF (A$ = "Y") OR (A$ = "y")  THEN 5850 ELSE  5870
5850 GOTO 5780
5870 INPUT "NUMBER OF DISCONTINOUS SHORT EDGES (0,1 OR 2) = ",NS
5880 INPUT "NUMBER OF DISCONTINOUS LONG EDGES (0,1 OR 2)  = ",NL
5890 ED1= TS -CR -5
5900 IF WS1 > ((LX/12) * 1000) THEN 5910 ELSE  5930
5910 LX = LX1 : LY = LY1
5920 GOTO  5960
5930 LX = LX1 +(ED1/1000)
5940 IF LY/LX >2 THEN 5950 ELSE 5960
5950 COLOR 4:PRINT TAB(10)"DESIGN AS ONE WAY SLAB":GOTO 9400
5960 SL = TS * .025
5970 DL = SL + FL
5980 TL = DL + LL
5990 ND=NS+NL
6000 K1=4/3 : K2=4/3 : K3=4/3 : K4=4/3
6010 IF NS=1 THEN K2=0
6020 IF NS=2 THEN K1=0 : K2=0
6030 IF NL=1 THEN K4=0
6040 IF NL=2 THEN K3=0 : K4=0
6050 YB= (24+2*ND+1.5*ND^2)/1000 : REM *** LONG SPAN COEFFICENT
6060 G=2/9 *(3-SQR (18)*(LX/LY)*(SQR (YB+K1*YB)+SQR (YB+K2*YB)))
6070 XB=G/(SQR (1+K3)+SQR (1+K4))^2 : REM ****SHORT SPAN COEFFICENT
6080 INPUT  " PARTITION WALL PARALLAL TO LY OR LX (0,1,2)  = ", PW
6090 IF PW=0 THEN  6190 ELSE  6100
6100 IF PW=1 THEN  6110 ELSE 6150
6110 INPUT  " HEIGHT OF PARTITION WALL IN METERS   = ", H
6120 GOSUB 7340
6130 MA3 = ML3:MYA = MYL: MA1 = ML1: MXA = MXL
6140 GOTO  6190
6150 IF PW=2 THEN  6160
6160 INPUT  " HEIGHT OF PARTITION WALL IN METERS           = ", H
6170 GOSUB 7810
6180 MA3 = MB3: MYA = MYB: MA1 = MB1: MXA = MXB
6190 AMX=XB*TL*LX^2
6200 MXP = INT ((AMX + MXA) * 1.5 * 100)/100
6210 AM3=K3*AMX
6220 MXN = INT ((AM3 + MA3) * 1.5 * 100)/100
6230 AMY=YB*TL*LX^2
6240 MYP = INT ((AMY + MYA) * 1.5 * 100)/100
6250 AM1=K1*AMY
6260 MYN = INT ((AM1 + MA1) * 1.5 * 100)/100
6270 MU = MXP:ED=ED1: GOSUB 8290
6280 ASM = AST
6290 MU = MXN :ED = ED1: GOSUB 8290
6300 ASS = AST
6310 MU = MYP :ED =(ED1-10): GOSUB 8290
6320 ALM = AST
6330 MU = MYN :ED =(ED1-10): GOSUB 8290
6340 ALS = AST
6350 ANX = XB * DL * LX^2 +(MXA/1.5)
6360 AN3 = K3 * ANX +(MA3/1.5)
6370 COLOR 2:INPUT  " DO YOU WANT TO CHECK FOR DEFLECTION (Y\N)    =", C$
6380 IF ( C$ = "Y") OR (C$ = "y") THEN 6390 ELSE 6500
6390 D = TS:BW = 1000:MF1 = ((TL +W1)/2)* LX^2/12: MF2 = MF1:M2 = AMX
6400 L = LX: D1 = ED1: MF3 = (( DL + W1 )/2) *LX^2 /12:MF4 = MF3:M5 = ANX:PT2=ASM/(D1 *10)
6410 IF NS = 0 THEN GOSUB 8360 : GOTO 6440
6420 IF NS = 1 THEN GOSUB 8410 : GOTO 6440
6430 IF NS = 2 THEN  GOSUB 8460
6440 ADFL = L * 1000 /250
6450 IF AT <ADFL GOTO 6460 ELSE 6480
6460 CLS:COLOR 2:PRINT TAB(10) " DEPTH PROVIDED IS OK FOR DEFLECTION "
6470 GOTO  6500
6480 COLOR 4:PRINT TAB(10) "CHANGE THE DEPTH FOR DEFLECTION "
6485 COLOR 15:PRINT TAB(13) "ALLOWABLE DEFLECTION   .   .   = ",ADFL
6488 PRINT TAB(13) "ANTICIPATED DEFLECTION .   .   = ",AT
6490 GOTO 7330
6500 PRINT TAB(15)"DESIGN OF TWO WAY SLAB (ROOF/FLOOR SLAB)=",T$
6510 PRINT
6520 PRINT TAB(45)"PANEL SIZE        = ";:PRINT USING "##.##";LX1;:PRINT " X ";:PRINT USING "#.##";LY1;:PRINT " M"
6530 PRINT TAB(45)"THICKNESS OF SLAB = ";TS;"MM"
6540 PRINT TAB(5)"LOADINGS"
6550 PRINT
6560 PRINT TAB(5) "SELF WEIGHT               = ";:PRINT USING "##.##";SL;:PRINT" Kn/Sq.M"
6570 PRINT TAB(5) "FLOOR FINISH /WEARTHERING COURSE + LIGHT PARTITION(IF ANY) = ";:PRINT USING "##.##";FL;:PRINT" Kn/Sq.M"
6580 PRINT TAB(5) "IMPOSED (LIVE) LOAD       = ";:PRINT USING "##.##";LL;:PRINT" Kn/Sq.M"
6590 PRINT
6600 PRINT TAB(10)"TOTAL LOAD           = ";:PRINT USING "##.##";TL;:PRINT" Kn/Sq.M"
6610 PRINT TAB(5)"DESIGN"
6620 PRINT
6630 PRINT TAB(30)"SHORT SPAN";:PRINT TAB(55)"LONG SPAN"
6640 PRINT
6650 PRINT TAB(25)"SUPPORT";:PRINT TAB(35)"MID SPAN";: PRINT TAB(50) "SUPPORT";:PRINT TAB(60)"MID SPAN"
6660 PRINT
6670 PRINT TAB(5) "BENDING MOMENT";:PRINT TAB(25);:PRINT USING "##.##";MXN;:PRINT TAB(37);:PRINT USING "##.##";MXP;:PRINT TAB(52);:PRINT USING "##.##";MYN;:PRINT TAB(62);:PRINT USING "##.##";MYP
6680 PRINT TAB(10)"(MU) Kn-M"
6690 PRINT
6700 PRINT TAB(5) "AREA OF STEEL";:PRINT TAB(25);:PRINT USING "##.##";ASS;:PRINT TAB(37);:PRINT USING "##.##";ASM;:PRINT TAB(52);:PRINT USING "##.##";ALS;:PRINT TAB(62);:PRINT USING "##.##";ALM
6710 PRINT TAB(10)"(Ast) Sq.CM"
6720 COLOR 15:INPUT "DO YOU WANT TO TAKE PRINT  OUT ? ( Y / N )     = ",A$
6730 IF A$ = "Y" OR A$ ="y" THEN 6740 ELSE 9420
6740 LPRINT CHR$(27)"!"CHR$(0);:LPRINT CHR$(27)"~80";:LPRINT CHR$(27)"l"CHR$(5);:LPRINT CHR$(27)"Q"CHR$(80)
6750 LPRINT TAB(12)"DESIGN OF TWO WAY SLAB (ROOF/FLOOR SLAB)=",T$
6760 LPRINT
6770 LPRINT TAB(40)"PANEL SIZE     = ";:LPRINT USING "##.##";LX1;:LPRINT " X ";:LPRINT USING "#.##";LY1;:LPRINT "M"
6780 LPRINT TAB(40)"SLAB THICKNESS = ";TS;"MM"
6790 LPRINT TAB(40)"CLEAR COVER    = ";CR;" MM"
6800 LPRINT TAB(2)"LOADINGS"
6810 LPRINT
6820 LPRINT TAB(7) "SELF WEIGHT.   .   .   .   .   . = ";:LPRINT USING "##.##";SL;:LPRINT" Kn/Sq.M"
6830 LPRINT TAB(7) "FLOOR FINISH /WEARTHERING COURSE"
6840 LPRINT TAB(12)"+ LIGHT PARTITION(IF ANY)   = ";:LPRINT USING "##.##";FL;:LPRINT" Kn/Sq.M"
6850 LPRINT TAB(7) "IMPOSED (LIVE) LOAD    .   .   . = ";:LPRINT USING "##.##";LL;:LPRINT" Kn/Sq.M"
6860 LPRINT
6870 LPRINT TAB(12)"TOTAL LOAD .   .   .   .    = ";:LPRINT USING "##.##";TL;:LPRINT" Kn/Sq.M"
6880 LPRINT
6890 LPRINT:LPRINT TAB(2)"EDGE CONDITION   .   .   .  = ";
6900 IF NS = 0 THEN 6910 ELSE 6940
6910 IF NL=0 THEN LPRINT "ALL EDGES CONTINUOUS"
6920 IF NL=1 THEN LPRINT "ONE LONG EDGE DISCONTINOUS"
6930 IF NL=2 THEN LPRINT "TWO SHORT EDGES CONTINUOUS"
6940 IF NS=1 THEN 6950 ELSE 6980
6950 IF NL=0 THEN LPRINT "ONE SHORT EDGE DISCONTINUOUS"
6960 IF NL=1 THEN LPRINT "TWO ADJACENT EDGES CONTINUOUS"
6970 IF NL=2 THEN LPRINT "ONE SHORT EDGE CONTINUOUS"
6980 IF NS=2 THEN  6990 ELSE 7020
6990 IF NL=0 THEN LPRINT "TWO LONG EDGES CONTINUOUS"
7000 IF NL=1 THEN LPRINT "ONE LONG EDGE CONTINUOUS"
7010 IF NL=2 THEN LPRINT "ALL EDGES DISCONTINUOUS"
7020 LPRINT TAB(2)"CHARACTERISTIC STRENGTH OF "
7030 LPRINT TAB(12)         " CONCRETE, STEEL .   .   .   .   .  = "; FCK;:LPRINT " , ";:LPRINT; FY
7040 IF PW = 1 THEN 7050 ELSE 7070
7050 LPRINT TAB(2) "HEIGHT OF PARTITION WALL   .   .   .    .     = ";:LPRINT USING "##.##";H;:LPRINT "  M"
7060 LPRINT TAB(2) "POSITION OF WALL NEARER TO LY  .   .    .     = ";:LPRINT USING "##.##";B;:LPRINT "  M"
7070 IF PW = 2 THEN 7080 ELSE 7100
7080 LPRINT TAB(2) "HEIGHT OF PARTITION WALL   .   .    .    .    = ";:LPRINT USING "##.##";H;:LPRINT "  M"
7090 LPRINT TAB(2) "POSITION OF WALL NEARER TO LX                 = ";:LPRINT USING "##.##";A;:LPRINT "  M"
7100 LPRINT:LPRINT
7110 LPRINT TAB(2)"DESIGN"
7120 LPRINT
7130 LPRINT TAB(27)"SHORT SPAN";:LPRINT TAB(52)"LONG SPAN"
7140 LPRINT
7150 LPRINT TAB(22)"SUPPORT";:LPRINT TAB(32)"MID SPAN";: LPRINT TAB(47) "SUPPORT";:LPRINT TAB(57)"MID SPAN"
7160 LPRINT
7170 LPRINT TAB(2) "BENDING MOMENT";:LPRINT TAB(22);:LPRINT USING "##.##";MXN;:LPRINT TAB(34);:LPRINT USING "##.##";MXP;:LPRINT TAB(49);:LPRINT USING "##.##";MYN;:LPRINT TAB(59);:LPRINT USING "##.##";MYP
7180 LPRINT TAB(9) " ( MU ) "
7190 LPRINT
7200 LPRINT TAB(2) "AREA OF STEEL";:LPRINT TAB(22);:LPRINT USING "##.##";ASS;:LPRINT TAB(34);:LPRINT USING "##.##";ASM;:LPRINT TAB(49);:LPRINT USING "##.##";ALS;:LPRINT TAB(59);:LPRINT USING "##.##";ALM
7210 LPRINT TAB(7)"(Ast) Sq.CM"
7220 LPRINT
7230 LPRINT TAB(2)"Reinforcement"
7240 LPRINT TAB(2)"Details"
7250 LPRINT TAB(2)"(Main bars)"
7260 LPRINT :LPRINT
7270 LPRINT
7280 LPRINT
7290 IF ( C$ = "Y") OR (C$ = "y") THEN 7300 ELSE 7330
7300 LPRINT TAB(2) "ALLOWABLE DEFLECTION ";:LPRINT  TAB(30);:LPRINT USING "##.#";ADFL;:LPRINT "  MM"
7310 LPRINT TAB(2) "ANTICIPATED DEFLECTION ";:LPRINT  TAB(30);:LPRINT USING "##.#";AT;:LPRINT "  MM"
7320 LPRINT TAB(2)"NOTE-  C-Cranked bars, E-Extra bars"
7330 RETURN
7340 REM - PARTION WALL PARRALEL TO LY
7350 INPUT "POSITION OF WALL NEARER TO LY IN METER = ",B
7360 W=.115*H*20
7370 W1=(W/LX)
7380  P = LY/LX
7390 B1=B/(LX/2)
7400 IF P = 1 THEN 7410 ELSE  7430
7410 FBL = 1.23
7420 FLL = 1.23:GOTO 7760
7430 IF P <= 1.1 THEN 7440 ELSE  7460
7440 FBL = 1.23 + (.27/.1 )* ( K-1!)
7450 FLL = 1.23 + (9.000001E-02/.1)* (K -1!): GOTO 7760
7460 IF P <= 1.2 THEN 7470 ELSE  7490
7470 FBL = 1.5 + (.25/.1 )* ( K-1.1 )
7480 FLL = 1.32 + (.08/.1)*(K-1.1) : GOTO 7760
7490 IF P <= 1.25 THEN 7500 ELSE  7520
7500 FBL = 1.75 + (.15/.1 )* ( K-1.2 )
7510 FLL = 1.4 + (.05/.1)*(K-1.2):GOTO 7760
7520 IF P <= 1.3 THEN 7530 ELSE  7550
7530 FBL = 1.9
7540 FLL = 1.45 : GOTO 7760
7550 IF P <= 1.4 THEN 7560 ELSE  7580
7560 FBL = 1.9
7570 FLL = 1.45 + ((-)*.15/.1)*(K-1.3):GOTO 7760
7580 IF P <= 1.5 THEN 7590 ELSE  7610
7590 FBL = 1.9
7600 FLL = 1.3 + ((-)*.02/.1)*(K-1.4):GOTO 7760
7610 IF P <= 1.6 THEN 7620 ELSE  7640
7620 FBL = 1.9
7630 FLL = 1.28 + ((-)*.01/.1)*(K-1.5):GOTO 7760
7640 IF P <= 1.7 THEN 7650 ELSE  7670
7650 FBL = 1.9
7660 FLL = 1.27 + ((-)*.02/.1)*(K-1.6):GOTO 7760
7670 IF P <= 1.8 THEN 7680 ELSE  7700
7680 FBL = 1.9
7690 FLL = 1.25 + ((-)*.05/.1)*(K-1.7):GOTO 7760
7700 IF P <= 1.9 THEN 7710 ELSE 7730
7710 FBL = 1.9
7720 FLL = 1.2 + ((-)*.08/.1)*(K-1.8):GOTO 7760
7730 IF P <= 2  THEN 7740 ELSE 7760
7740 FBL = 1.9
7750 FLL = 1.12 + ((-)*.07/.1)*(K-1.9)
7760 MXL =XB * W1 *FBL * (LX)^2*B1
7770 ML3 = K3 * MXL
7780 MYL =YB * W1 * FLL *(LX)^2 *B1
7790 ML1 = K1 * MYL
7800 RETURN
7810 REM - PARTION WALL PARRALEL TO LX
7820 INPUT "POSITION OF WALL NEARER TO LX IN METER = ",A
7830 A1=A/(LY/2)
7840 W=.115*H*20
7850 W1=(W/LY)
7860  P = LY/LX
7870 IF P = 1 THEN 7880 ELSE  7900
7880 FBB = 1.23:FLB = 1.23
7890 GOTO 8230
7900 IF P <= 1.1 THEN 7910 ELSE  7930
7910 FBB = 1.23 + (.12/.1 )* ( K-1!)
7920 FLB = 1.23 + (.43/.1)* (K -1!): GOTO 8230
7930 IF P <= 1.2 THEN 7940 ELSE  7960
7940 FBB = 1.35 + (.1/.1 )* ( K-1.1 )
7950 FLB = 1.66 + (.34/.1)*(K-1.1) : GOTO 8230
7960 IF P <= 1.25 THEN 7970 ELSE  7990
7970 FBB = 1.45 + (.08/.1 )* ( K-1.2 )
7980 FLB = 2+ (.2/.1)*(K-1.2):GOTO 8230
7990 IF P <= 1.3 THEN 8000 ELSE  8020
8000 FBB = 1.53 + (O.02/.1)*(K-1.25)
8010 FLB = 2.2 +(.2/.1)*(K-1.25) : GOTO 8230
8020 IF P <= 1.4 THEN 8030 ELSE  8050
8030 FBB = 1.55 +(.05/.1) * (K-1.3)
8040 FLB = 2.4 + (.3/.1)*(K-1.3):GOTO 8230
8050 IF P <= 1.5 THEN 8060 ELSE  8080
8060 FBB = 1.6 + (O.O5/.1) * (K-1.4)
8070 FLB = 2.7 + (.1/.1)*(K-1.4):GOTO 8230
8080 IF P <= 1.6 THEN 8090 ELSE  8110
8090 FBB = 1.65 + (.05/.1) * (K-1.5)
8100 FLB = 2.8 + (.1/.1)*(K-1.5):GOTO 8230
8110 IF P <= 1.7 THEN 8120 ELSE  8140
8120 FBB = 1.7 + (.05/.1) * (K-1.6)
8130 FLB = 2.9 + (.1/.1)*(K-1.6):GOTO 8230
8140 IF P <= 1.8 THEN 8150 ELSE  8170
8150 FBB = 1.75 + (.08/.1) * (K-1.7)
8160 FLB = 3 + (.3/.1)*(K-1.7):GOTO 8230
8170 IF P <= 1.9 THEN 8180 ELSE 8200
8180 FBB = 1.83 + (9.000001E-02/.1) * (K-1.8)
8190 FLB = 3.3 + (.3/.1) * (K-1.8) : GOTO  8230
8200 IF P<= 2 THEN 8210 ELSE  8230
8210 FBB = 1.92 + (.08/.1) * (K-1.9)
8220 FLB = 3.6 + (.4/.1) * (K-1.9)
8230 MXB=XB*W1*FBB*LX^2*A1
8240 MB3=K3*MXB
8250 MYB=YB*W1*FLB*LX^2*A1
8260 MB1=K1*MYB
8270 RETURN
8280 REM CALCULATION OF STEEL
8290 AST =.5 * FCK * ED * 1000 * (1 - (1 - (4.6 * MU * 10^6)/(FCK * ED^2*10^3))^.5)/FY/100
8300 ASTM = TS * .012
8310 IF AST < ASTM THEN 8320 ELSE 8330
8320 AST = ASTM
8330 AST = AST
8340 RETURN
8350 REM CALCULATION OF DEFLECTIN FOR TWO SHORT EDGES CONTINUOUS CONDITION
8360 K3 = .0625: M1 = AM3: M3 = M1
8370 M4 = AN3: M6 =M4
8380 PT1 = ASS/(10 * D1): PT3 =PT1
8390 GOTO 8480
8400 REM CALCULATION OF DEFLECTIN FOR ONE SHORT EDGE CONTINUOUS CONDITION
8410 M1 =0: M3 = AM3: M4 = 0:  M6 = AN3: PT1 = 0
8420 PT3 =ASS/(D1 * 10)
8430 K3 = .086
8440 GOTO  8480
8450 REM CALCULATION OF DEFLECTIN FOR TWO SHORT EDGES DISCONTINUOUS CONDITION
8460 K3 = .125: M1= 0:  M3 = 0: M4 = 0: M6 = 0
8470 PT1 = 0: PT3 = 0
8480 MA = MO: PTA = PT1:  GOSUB 8990
8490 IRL = IRA : XL = XA: ZL = ZA
8500 IGR = 1000*(TS)^3/12
8510 MR = FCR * IGR/(D/2)
8520 MA = MO : PTA = PT2 : GOSUB 8990
8530  IRM = IRA :XM = XA: ZM = ZA
8540 MA = MO : PTA = PT3 : GOSUB 8990
8550  IRR = IRA :XR = XA: ZR = ZA
8560 AK2 = (M1+M3)*(1/(MF1+MF2))
8570 IF AK2 <= .5 THEN  8580 ELSE 8590
8580 AK1 = 0: GOTO 8610
8590 K2 = AK2: GOSUB 9070
8600 AK1 = K1
8610 IRE = AK1 * ((IRL + IRR)/2) + (1-AK1)*IRM
8620 MRA = MR: MB = M2: Z = ZM: X = XM: IRA = IRE: GOSUB 9300
8630 IEFT = IEFF
8640 BK2 = (M4 + M6)*(1/(MF3 +MF4))
8650 IF BK2 <= .5 THEN 8660 ELSE  8670
8660 BK1 = 0 : GOTO  8830
8670 K2  = BK2 :GOSUB 9070
8680 BK1 = K1
8690 IRP = BK1 * (IRL + IRR)/2 + (1-BK1) * IRM
8700 MRA = MR:MB = M5: Z = ZM: X= XM: IRA = IRP:D1 = D1: GOSUB 9300
8710 IEFP = IEFF
8720  REM CALCULATION OF SHORT TERM DEFLECTIONS
8730 AIT =(L^2 *10^12/(EC * IEFT))* ((5/48) * M2 - (M1/16) - (M3/16))
8740 AIP =( L^2 *10^12/(EC * IEFP))* ((5/48) * M5 - (M4/16) - (M6/16))
8750 AI = AIT - AIP
8760 REM CALCULATION OF SHRINKAGE DEFLECTION
8770 PTM = PT2 * 100
8780 IF PTM  < 1  THEN 8790 ELSE 8810
8790 K4 = .72 *(PTM)^.5
8800 GOTO 8820
8810 K4 = .65 *(PTM)^.5
8820 IF K4 > 1 THEN  K4 = 1
8830 YCS = K4  * ECS/(D)
8840 ACS = K3 * YCS * L^2*10^6
8850 REM DEFLECTION DUE TO CREEP
8860 MA = MOR: PTA = PT1: GOSUB 8990
8870 IRCL = IRA: XCL = XA: ZCL = ZA
8880 MA = MOR: PTA = PT2: GOSUB 8990
8890 IRCM = IRA: XCM = XA :ZCM = ZA
8900 MA = MOR: PTA = PT3: GOSUB 8990
8910 IRCR = IRA: XCR = XA:ZCR = ZA
8920 IRCE = BK1 * (IRCL + IRCR)/2 + (1-BK1) * IRCM
8930 MRA = MR: MB = M5 : IRA = IRCE: Z = ZCM: X = XCM:GOSUB 9300
8940 IEFC = IEFF
8950  AICP =(L^2*10^12/(ECE*IEFC))* ((5/48)*M5 - (M4/16) -(M6/16))
8960 ACP = AICP - AIP
8970 REM TOTAL DEFLECTION
8980 AT = AIT + ACP +ACS
8990 REM SUBROUTINE FOR CALCULATING IR
9000 F1 = MA * PTA
9010 F = (F1^2 + 2 * F1)^.5 - F1
9020 KTA = F^3/3 + F1 * (1-F)^2
9030 IRA = KTA *1000*D1^3
9040 XA = F*D1
9050 ZA = D1 -(XA/3)
9060 RETURN
9070 REM SUBROUTINE FOR CALCULATING K1
9080 IF K2 <= .5 THEN 9090 ELSE  9100
9090 K1 = 0 : GOTO  9290
9100 IF K2 <= .6 THEN 9110 ELSE  9120
9110 K1 = O + (.03/.1)*(K2-.5) : GOTO  9290
9120 IF K2 <= .7 THEN 9130 ELSE  9140
9130 K1 = .03 + (.05/.1)*(K2-.6) : GOTO  9290
9140 IF K2 <= .8 THEN 9150 ELSE  9160
9150 K1 = .08 + (.08/.1)*(K2-.7) : GOTO  9290
9160 IF K2 <= .9 THEN 9170 ELSE  9180
9170 K1 = .16 + (.14/.1)*(K2-.8) : GOTO  9290
9180 IF K2 <= 1 THEN 9190 ELSE  9200
9190 K1 = .3 + (.2/.1)*(K2-.9) : GOTO  9290
9200 IF K2 <= 1.1 THEN 9210 ELSE 9220
9210 K1 = .5 + (.23/.1)*(K2-1) : GOTO  9290
9220 IF K2 <= 1.2 THEN 9230 ELSE  9240
9230 K1 = .73 + (.18/.1)*(K2-1.1) : GOTO  9290
9240 IF K2 <= 1.3 THEN 9250 ELSE  9270
9250 K1 = .91 + (.06/.1)*(K2-1.2) : GOTO  9290
9260 IF K2 <= 1.4 THEN 9270 ELSE  9280
9270 K1 = .97 + (.03/.1)*(K2-1.3) : GOTO  9290
9280 IF K2 > 1.4 THEN K1 = 1!
9290  RETURN
9300 REM SUBROUTINE FOR CALCULATION OF IEFF
9310 S1=MRA/(MB*10^6)
9320 S2= Z/(D1 )
9330 S3=1-X/(D1 )
9340 S4=1.2-(S1*S2*S3)
9350 IF S4 >= 1 THEN 9360 ELSE 9370
9360 IEFF = IRA : GOTO 9400
9370 IF S4 < 0 THEN 9380 ELSE 9390
9380 IEFC = IGR :GOTO  9400
9390 IEFF = IRA/S4
9400 RETURN
9420 END
